buildscript {
	repositories {
		jcenter()
		google()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:3.0.1'
	}
}

apply plugin: 'com.android.application'

repositories {
	jcenter()
	mavenCentral()
}

dependencies {
	implementation 'com.android.support:support-annotations:22.2.0' // https://sites.google.com/a/android.com/tools/tech-docs/support-annotations
	implementation 'org.mapsforge:mapsforge-map-android:0.9.+' // https://github.com/mapsforge/mapsforge
	implementation 'org.mapsforge:mapsforge-themes:0.9.+'
	implementation ('com.googlecode.json-simple:json-simple:1.1.1') { exclude module: 'junit' }
}

android {
	//
	// To sign release builds, create the file `~/.gradle/gradle.properties`
	// (or in your project directory) with this content:
	//
	// mStoreFile=/path/to/key.store
	// mStorePassword=xxx
	// mKeyAlias=alias
	// mKeyPassword=xxx
	//
	if (project.hasProperty('mStoreFile') &&
			project.hasProperty('mStorePassword') &&
			project.hasProperty('mKeyAlias') &&
			project.hasProperty('mKeyPassword')) {
		signingConfigs {
			release {
				storeFile file(mStoreFile)
					storePassword mStorePassword
					keyAlias mKeyAlias
					keyPassword mKeyPassword
			}
		}
		buildTypes.release.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	compileSdkVersion 25
	buildToolsVersion '26.0.2'
	defaultConfig {
		minSdkVersion 25
		targetSdkVersion 25
		versionCode 20010
		versionName "2.alpha.amber"
	}
	lintOptions {
		abortOnError false
		disable 'MissingTranslation'
	}
}

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:-deprecation"
		}
	}
}
