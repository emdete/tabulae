buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:+'
	}
}

apply plugin: 'com.android.application'

repositories {
	jcenter()
	mavenCentral()
	maven {
		url "http://maven.rushorm.com"
	}
}

dependencies {
	compile 'com.android.support:support-annotations:22.2.0' // https://sites.google.com/a/android.com/tools/tech-docs/support-annotations
	// compile 'com.android.support:support-v23:0.0.1' // https://developer.android.com/tools/support-library/features.html
	compile 'org.mapsforge:mapsforge-map-android:0.6.+'
	compile ('com.googlecode.json-simple:json-simple:1.1.1') { exclude module: 'junit' }
}

android {
	//
	// To sign release builds, create the file `~/.gradle/gradle.properties`
	// (or in your project directory) with this content:
	//
	// mStoreFile=/path/to/key.store
	// mStorePassword=xxx
	// mKeyAlias=alias
	// mKeyPassword=xxx
	//
	if (project.hasProperty('mStoreFile') &&
			project.hasProperty('mStorePassword') &&
			project.hasProperty('mKeyAlias') &&
			project.hasProperty('mKeyPassword')) {
		signingConfigs {
			release {
				storeFile file(mStoreFile)
					storePassword mStorePassword
					keyAlias mKeyAlias
					keyPassword mKeyPassword
			}
		}
		buildTypes.release.signingConfig = signingConfigs.release
	} else {
		buildTypes.release.signingConfig = null
	}

	compileSdkVersion 23
	buildToolsVersion "21.1.2"
	defaultConfig {
		minSdkVersion 19
		targetSdkVersion 23
		versionCode 20009
		versionName "2.alpha.midnight"
	}
	lintOptions {
		abortOnError false
		disable 'MissingTranslation'
	}
}

allprojects {
	gradle.projectsEvaluated {
		tasks.withType(JavaCompile) {
			options.compilerArgs << "-Xlint:-deprecation"
		}
	}
}
